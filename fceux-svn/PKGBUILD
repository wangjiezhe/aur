# Maintainer: Lukas Sabota <LTsmooth42 _at_ gmail _dot_ com>

_pkgname=fceux
pkgname=fceux-svn
pkgver=3372
pkgrel=1
pkgdesc="Fast and ultra-compatible NES/Famicom emulator with SDL, OpenGL and SVGALIB support"
arch=('i686' 'x86_64')
url="http://fceux.com/"
license=('GPL')
depends=('sdl' 'lua51' 'gtk2' 'gd' 'libgl')
makedepends=('subversion' 'scons' 'libgl' 'glu')
optdepends=('xchm: for viewing the help manual')
provides=('fceu' 'gfceux' 'fceux')
conflicts=('fceu' 'gfceux' 'fceux')
source=(${_pkgname}::svn://svn.code.sf.net/p/fceultra/code/fceu/trunk)
md5sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  svn info --show-item revision | sed 's/ //g'
}

prepare() {
  rm -rf ${_pkgname}-build
  svn export ${_pkgname} ${_pkgname}-build
}

build(){
  cd ${_pkgname}-build

  scons RELEASE=1
}

package() {
  cd ${_pkgname}-build

  install -D -m755 bin/fceux "$pkgdir/usr/bin/fceux"
  install -D -m755 bin/fceux-net-server "$pkgdir/usr/bin/fceux-net-server"
  install -D -m644 bin/fceux.chm "$pkgdir/usr/share/doc/fceux/fceux.chm"
  install -D -m644 "fceux.desktop" "$pkgdir/usr/share/applications/fceux.desktop"
  install -D -m644 "fceux.png" "$pkgdir/usr/share/pixmaps/fceux.png"
}
