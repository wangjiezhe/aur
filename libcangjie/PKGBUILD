# Maintainer: wangjiezhe <wangjiezhe AT yandex DOT com>
pkgname=libcangjie-git
_pkgname=libcangjie
pkgver=1.3.1.gf33e16c
pkgrel=1
pkgdesc="A C library implementing the Cangjie input method"
arch=('i686' 'x86_64')
url="https://github.com/Cangjians/libcangjie"
license=('GPLv3')
depends=('sqlite')
makedepends=('git')
provides=('libcangjie')
conflicts=('libcangjie')
options=('libtool')
source=(${_pkgname}::git+https://github.com/Cangjians/libcangjie.git#branch=1.x
        automake.patch)
md5sums=('SKIP'
         '6bdfe2033ff09d9193eecc6dc8664d3a')

pkgver() {
  cd ${_pkgname}
  git describe --tags| sed 's/^v//;s/-/./g'
}

prepare() {
  cd ${_pkgname}
  patch -Np0 -i ../automake.patch
}

build() {
  cd ${_pkgname}

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd ${_pkgname}

  make DESTDIR=${pkgdir} install
}

# vim:set ts=2 sw=2 et:
