# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Rémy Oudompheng <remy@archlinux.org>
# Contributor: wangjiezhe <wangjiezhe AT yandex DOT com>

pkgname=gfan
pkgver=0.5
pkgrel=2
pkgdesc="A software package for computing Gröbner fans and tropical varieties"
arch=('i686' 'x86_64')
url="http://home.imf.au.dk/jensen/software/gfan/gfan.html"
license=('GPL')
depends=('cddlib')
source=("http://home.imf.au.dk/jensen/software/gfan/gfan${pkgver}.tar.gz" 'fix-build.patch')
md5sums=('2d76d1625e0766c57c2b3ece809c23c8'
         'e327ec23a3bdf20ce6c8711ab154db50')

prepare() {
  cd gfan$pkgver
  patch -p1 -i $srcdir/fix-build.patch
}

build() {
  cd gfan$pkgver
  make
  cp gfanlib.h gfanlib/
  cp gfanlib_* gfanlib/
  cd gfanlib
  ./configure --prefix=/usr
  make
  cd ../doc
  make
}

package() {
  cd gfan$pkgver
  make PREFIX="$pkgdir"/usr install
  install -dm755 "$pkgdir"/usr/share/doc/gfan
  install -dm755 "$pkgdir"/usr/include/gfanlib
  install -dm755 "$pkgdir"/usr/lib
  install -Dm644 doc/manual.pdf doc/manual.ps "$pkgdir"/usr/share/doc/gfan
  install -Dm644 gfanlib/gfanlib*.h "$pkgdir"/usr/include/gfanlib
  install -Dm644 gfanlib/libgfan.a "$pkgdir"/usr/lib/libgfan.a
}
