# Maintainer: wangjiezhe <wangjiezhe AT yandex DOT com>

_pkgname=ChezScheme
pkgname=chez-scheme-git
_pkgver=9.4.1
pkgver=r143.e580267
pkgrel=1
pkgdesc="Chez Scheme is an implementation of the Revised6 Report on Scheme [27] (R6RS) with numerous language and programming environment extension. (threaded build)"
arch=(i686 x86_64)
url="https://github.com/cisco/ChezScheme"
license=('APL')
makedepends=('git' 'ncurses' 'libx11' 'xproto')
options=('!ccache')
provides=(chez-scheme)
conflicts=(petite-chez-scheme)
replaces=(petite-chez-scheme)
source=(${_pkgname}::git+https://github.com/cisco/ChezScheme.git)
md5sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  # git describe --tags --long | sed 's/^v//g;s/-/./g'
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_pkgname}
  sed -i "s/ln -sf/ln -f/g" makefiles/Mf-install.in
  sed -i "s/\\\INSERTREVISIONMONTHSPACEYEAR/May 2016/g" csug/{csug,copyright}.stex
}

build() {
  cd ${_pkgname}
  ./configure --installprefix=/usr --temproot=${pkgdir} --threads
  make
  make docs
}

package() {
  cd ${_pkgname}
  make DESTDIR=${pkgdir} install InstallSchemeName=chez-scheme

  docdir=${pkgdir}/usr/share/doc/chez-scheme
  install -Dm644 -t ${docdir} \
          csug/csug.pdf release_notes/release_notes.pdf
  install -Dm644 -t ${docdir}/html/release_notes \
          release_notes/{release_notes.html,releasenotes.css}
  install -Dm644 -t ${docdir}/html/csug \
          csug/{*.html,*.css}
  install -Dm644 -t ${docdir}/html/csug/canned \
          csug/canned/*
  install -Dm644 -t ${docdir}/html/csug/gifs \
          csug/gifs/*.gif
  install -Dm644 -t ${docdir}/html/csug/math/csug \
          csug/math/csug/*.gif
  ln -f ${docdir}/html/csug/{csug,index}.html
}

# vim:set ts=2 sw=2 et:
