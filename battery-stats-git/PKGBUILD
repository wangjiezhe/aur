# Maintainer: Jiezhe Wang <wangjiezhe@gmail.com>

pkgname=battery-stats-git
_pkgname=battery-stats
pkgver=0.5.6
pkgrel=2
pkgdesc="Log battery charge, show gnuplot graphs."
arch=('any')
url="https://github.com/petterreinholdtsen/battery-stats.git"
license=('GPLv2')
makedepends=('git')
provides=('battery-stats')
conflicts=('battery-stats')
source=("${_pkgname}::git+https://github.com/petterreinholdtsen/battery-stats.git"
        "battery-stats-collector.service"
        "battery-stats-collector.timer")
sha256sums=('SKIP'
            '8d82053d545532b3b7c646cb9d4e840f160ee9aa2572355a8f218ee43db01e7c'
            '8885fc7512936a918a44a864a0ebc371b0394859132752419414fbbc2383d0f1')

pkgver() {
  cd ${_pkgname}
  git describe --tags | sed "s/-/./g"
}

prepare() {
  mkdir -p build
  sed "s/ \/lib/ \${CMAKE_INSTALL_PREFIX}\/lib/" \
      -i ${_pkgname}/data/CMakeLists.txt
  sed "s/sbin/bin/" \
      -i ${_pkgname}/src/CMakeLists.txt
  sed "s/AC/AC0/" \
      -i ${_pkgname}/src/battery-stats-collector
  sed "/set fit quiet/a \\\techo \"set fit logfile '\/dev\/null'\"" \
      -i ${_pkgname}/src/battery-graph.in
}

build() {
  cd build
  cmake ../${_pkgname} -DCMAKE_INSTALL_PREFIX=/usr
}

package() {
  install -Dm644 battery-stats-collector.service ${pkgdir}/usr/lib/systemd/system/battery-stats-collector.service
  install -Dm644 battery-stats-collector.timer ${pkgdir}/usr/lib/systemd/system/battery-stats-collector.timer
  cd build
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
