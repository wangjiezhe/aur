# Maintainer: Jiezhe Wang <wangjiezhe@gmail.com>

pkgname=battery-stats-git
_pkgname=battery-stats
pkgver=0.5.4.6.g8398c63
pkgrel=1
pkgdesc="Log battery charge, show gnuplot graphs."
arch=('any')
url="https://github.com/petterreinholdtsen/battery-stats.git"
license=('GPLv2')
provides=('battery-stats')
conflicts=('battery-stats')
source=("${_pkgname}::git+https://github.com/petterreinholdtsen/battery-stats.git"
        "battery-stats-collector.service")
sha256sums=('SKIP'
            '458937c0f81d21b45a6687a6a5155a3a3866d46d65203fb174ccc020fc612edf')

pkgver() {
  cd ${_pkgname}
  git describe | sed "s/-/./g"
}

prepare() {
  mkdir -p build
  sed "s/ \/lib/ \${CMAKE_INSTALL_PREFIX}\/lib/" \
      -i ${_pkgname}/data/CMakeLists.txt
  sed "s/sbin/bin/" \
      -i ${_pkgname}/src/CMakeLists.txt
  sed "s/AC/AC0/" \
      -i ${_pkgname}/src/battery-stats-collector
}

build() {
  cd build
  cmake ../${_pkgname} -DCMAKE_INSTALL_PREFIX=/usr
}

package() {
  install -Dm644 battery-stats-collector.service ${pkgdir}/usr/lib/systemd/system/battery-stats-collector.service
  cd build
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
