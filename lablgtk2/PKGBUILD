# $Id: PKGBUILD 288078 2017-02-05 12:55:08Z jgc $
#Maintainer: Juergen Hoetzel <juergen@archlinux.org>

pkgname=lablgtk2
pkgver=2.18.6
pkgrel=1
pkgdesc="An Objective Caml interface to gtk2"
arch=(x86_64)
license=('LGPL')
url="http://lablgtk.forge.ocamlcore.org/"
depends=('gtk2' 'librsvg' 'gtksourceview2' 'gnome-applets')
makedepends=('ocaml>=4.0.6' 'camlp4>=4.06')
optdepends=('ocaml: for using the tools')
DLAGENTS=('https::/usr/bin/curl -fLC - --insecure --retry 3 --retry-delay 3 -o %o %u')
source=(https://forge.ocamlcore.org/frs/download.php/1726/lablgtk-${pkgver}.tar.gz)
md5sums=('30e9eef159eb88db0dce2438a60a6402')
options=(!makeflags staticlibs)

build() {
  cd "${srcdir}/lablgtk-${pkgver}"
  ./configure --prefix=/usr \
    --with-glade \
    --with-gnomeui \
    --with-gnomecanvas \
    --without-panel \
    --with-gtkspell \
    --with-gtksourceview2
  make world
  make opt
}

package() {
  cd "${srcdir}/lablgtk-${pkgver}"
  make DESTDIR=${pkgdir} old-install
  install -m644 META "${pkgdir}/usr/lib/ocaml/lablgtk2/"
}
