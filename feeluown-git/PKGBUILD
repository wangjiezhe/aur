# Maintainer: wenLiangcan <boxeed at gmail dot com>

pkgname=feeluown-git
_pkgname=FeelUOwn
pkgver=9.1a.15.gec5ee8c
pkgrel=1
epoch=1
pkgdesc="个性化音乐服务 For Mac And Linux"
arch=("any")
url="https://github.com/cosven/FeelUOwn"
license=('GPL3')
depends=('python-pyqt5' 'python-requests' 'python-quamash' 'python-sqlalchemy' 'python-dbus' 'qt5-multimedia' 'xdg-utils' 'python-yaml')
optdepends=('vlc')
makedepends=('git')
provides=("feeluown")
conflicts=("feeluown")
source=('git+https://github.com/cosven/FeelUOwn.git')
md5sums=('SKIP')
install="${pkgname}.install"

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --long | sed 's/^v//;s/release./r/;s/-/./g'
}

build() {
  cd "$srcdir/$_pkgname"
  python setup.py build
}

package() {
  cd "$srcdir/$_pkgname"
  python setup.py install --root="$pkgdir"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
