# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=acpi_call-dkms
pkgver=1.1.0
pkgrel=5
pkgdesc='A linux kernel module that enables calls to ACPI methods through /proc/acpi/call'
arch=('any')
url='http://github.com/mkottman/acpi_call'
license=('GPL')
depends=('dkms')
makedepends=('markdown')
optdepends=('linux-headers: needed if using archlinux default kernel'
	    'linux-lts-headers: needed if using the archlinux lts kernel')
provides=('acpi_call')
conflicts=('acpi_call')
source=("acpi_call-v${pkgver}.tar.gz::http://github.com/mkottman/acpi_call/archive/v${pkgver}.tar.gz")
sha256sums=('d0d14b42944282724fca76f57d598eed794ef97448f387d1c489d85ad813f2f0')

prepare() {
	cd "${srcdir}/acpi_call-${pkgver}"

	# Fix build with Linux >= 3.17
	sed -i 's|acpi/acpi.h|linux/acpi.h|' acpi_call.c

	# create dkms.conf
	cat > dkms.conf <<EOF
# Linux DKMS config script for acpi_call

PACKAGE_VERSION=${pkgver}
PACKAGE_NAME=acpi_call
AUTOINSTALL=yes

BUILT_MODULE_NAME[0]="acpi_call"
DEST_MODULE_LOCATION[0]="/extra"
EOF
}

package() {
	cd "${srcdir}/acpi_call-${pkgver}"

	install -dm755 "${pkgdir}/usr/src/acpi_call-${pkgver}"
	install -m644 -t "${pkgdir}/usr/src/acpi_call-${pkgver}" \
		Makefile dkms.conf acpi_call.c

	install -dm755 "${pkgdir}"/usr/share/acpi_call
	cp -dr --no-preserve='ownership' {examples,support} "${pkgdir}"/usr/share/acpi_call/

	install -Dm644 README.md "${pkgdir}/usr/share/doc/acpi_call/README.md"

	install -dm755 "${pkgdir}/usr/lib/modules-load.d"
	echo acpi_call > "${pkgdir}"/usr/lib/modules-load.d/acpi_call.conf
}

